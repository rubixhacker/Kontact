apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    publishNonDefault true
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = "com.hackedcube"
            artifactId project.name
            version = project.version
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"


            pom.withXml {
                def root = asNode()


                // adds dependencies
                def dependenciesNode = root.appendNode('dependencies')
                configurations.compile.allDependencies.each { dp ->
                    if (dp.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dp.group)
                        dependencyNode.appendNode('artifactId', dp.name)
                        dependencyNode.appendNode('version', dp.version)
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    kapt "com.google.auto.value:auto-value:1.4"
    provided "com.google.auto.value:auto-value:1.4"
    kapt 'com.gabrielittner.auto.value:auto-value-with:1.0.0'
    kapt 'com.gabrielittner.auto.value:auto-value-cursor:1.0.1'
    compile 'com.gabrielittner.auto.value:auto-value-cursor-annotations:1.0.1'
}
repositories {
    mavenCentral()
}
